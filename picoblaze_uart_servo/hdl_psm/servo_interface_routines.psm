                      ;------------------------------------------------------------------------------------------
                      ; Pan/tilt servo driver
                      ; Copyright by Dung Le, 2012
                      ;------------------------------------------------------------------------------------------
                      ; Definition: simple routines for controlling servo. This driver incoporates with 
                      ; the hdl design in servo_controller.v. Currently support both continuous (full) rotation
                      ; servo and normal (0-180 degree) servo.
                      ;
                      ; NOTE: 
                      ; - Servo control register definition:
                      ;   + bit [5-0]: speed, 64 steps
                      ;   + bit 6: direction bit
                      ;   + bit 7: servo select bit
                      ; - The output pulse width from servo_controller unit are 0.5 - 2.5 ms for normal 
                      ;   servo, 1 - 2 ms for full rotation servo. PWM period is 16ms (fpga board run at 10MHZ)
                      ; 
                      ;------------------------------------------------------------------------------------------
                      ; Assign Scratch Pad Memory Locations
                      ;------------------------------------------------------------------------------------------
                      ;
                      CONSTANT CURRENT_PHI, 21      ; store current turn angle (phi) of servo 1, NORMALIZED TO 0-127 RANGE
                      CONSTANT MAX_SPEED, 3F
                      CONSTANT MAX_PHI, 7F
                      CONSTANT CENTER_POSITION, 3F
                      CONSTANT DIRECTION_BIT, 40
                      CONSTANT SERVO_SELECT_BIT, 80
                      ;
                      ;------------------------------------------------------------------------------------------
                      ; port declaration
                      ;------------------------------------------------------------------------------------------
                      ;
                      CONSTANT servo_port, 10
                      ;
                      ;------------------------------------------------------------------------------------------
                      ; SERVO ROUTINES
                      ;------------------------------------------------------------------------------------------
                      ;
                      ; USAGE:
                      ; - set up desired speed on s9
                      ; - call the appropriate routine for your need
                      ; 
                      ;
                      ; Registers used: s7,s8,s9
                      ;   s9 - input (speed), 6-bit (max 64 steps)
                      ;   s7,s8 - output regs
                      ;------------------------------------------------------------------------------------------
          servo_init: 
                      LOAD s9, CENTER_POSITION      ; center position - 90 deg
                      STORE s9, CURRENT_PHI
                      LOAD s9, 00
                      LOAD s8, 00
                      LOAD s7, 00
                      RETURN 
                      ;------------------------------------------------------------------------------------------
                      ; servo 0 is continuous rotation servo
                      ;------------------------------------------------------------------------------------------
  servo_0_turn_right: 
                      COMPARE s9, MAX_SPEED
                      RETURN NC                     ; return if speed greater than upper bound
                      OUTPUT s9, servo_port
                      RETURN 
                      ;
                      ;
                      ;
   servo_0_turn_left: 
                      COMPARE s9, MAX_SPEED
                      RETURN NC                     ; return if speed greater than upper bound
                      LOAD s7, s9
                      ADD s7, 40
                      OUTPUT s7, servo_port
                      RETURN 
                      ;------------------------------------------------------------------------------------------
                      ; servo 1 is normal 0-180 rotation servo
                      ;------------------------------------------------------------------------------------------
     servo_1_turn_up: 
                      COMPARE s9, MAX_SPEED
                      RETURN NC                     ; return if turn angle >= upper bound
                      FETCH s7, CURRENT_PHI         ; current phi from memory
                      ADD s7, s9                    ; current_phi = current_phi + turn angle
                      COMPARE s7, MAX_PHI           ; check if current_phi < MAX_PHI
                      JUMP C, calculate_turn_angle  ; yes, no need to fixup, jump
                      LOAD s7, MAX_PHI              ; else set current phi to MAX_PHI - 1
                      SUB s7, 01
                      JUMP calculate_turn_angle
                      ;
                      ;
                      ;
   servo_1_turn_down: 
                      COMPARE s9, MAX_SPEED
                      RETURN NC                     ; return if turn angle >= upper bound
                      FETCH s7, CURRENT_PHI         ; current phi from memory
                      SUB s7, s9                    ; current_phi = current_phi - turn angle
                      JUMP NC, calculate_turn_angle ; jump if current_phi >= 0
                      LOAD s7, 00                   ; else set current phi to 0
                      JUMP calculate_turn_angle
                      ;------------------------------------------------------------------------------------------
                      ; This section below is used by both above routines
                      ;------------------------------------------------------------------------------------------
calculate_turn_angle: 
                      STORE s7, CURRENT_PHI         ; save current_phi for later use
                      ;
                      ; now determine the bits that will be send out
                      ;
                      COMPARE s7, CENTER_POSITION   ; check if current_phi < CENTER_POSITION
                      JUMP NC, servo_1_label2       ; no, jump
                      LOAD s8, CENTER_POSITION      ;
                      SUB s8, s7                    ; output phi = CENTER_POSITION - currentphi
                      LOAD s7, s8
                      JUMP servo_1_label3
      servo_1_label2: 
                      SUB s7, CENTER_POSITION       ; else, output phi = current phi - CENTER_POSITION
                      ADD s7, DIRECTION_BIT         ; when current phi > CENTER_POSITION, must set DIRECTION BIT
      servo_1_label3: 
                      ADD s7, SERVO_SELECT_BIT      ; when use servo 1, must set SELECTION BIT
                      OUTPUT s7, servo_port         ; write to controller
                      RETURN 
